buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.+'
    }
}
apply plugin: 'android'

configurations {
    provided
}

android.applicationVariants.all {
    variant -> variant.javaCompile.classpath += configurations.provided
}

dependencies {
    // you need to install Android Support Repository from Extras in Android SDK Manager
    compile 'com.android.support:support-v4:13.0.0'
    // com.here.android.sdk.jar contains stubs only so we do not include it in the APK. actual implementations are
    // provided by the platform from com.here.android.jar library (see uses-library -element in AndroidManifest.xml)
    // the SDK library is deliberately placed outside of libs/ so that it does not get included in the APK automatically
    provided files( 'com.here.android.sdk.jar' )
}

android {
    compileSdkVersion 16
    buildToolsVersion "18.1.1"

    buildTypes {
        release {
            runProguard false
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-nokia-x.txt'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    lintOptions {
        // disable warning from missing class referenced in layout. the class is loaded from com.here.android.jar on runtime
        disable "MissingRegistered"
    }
}
